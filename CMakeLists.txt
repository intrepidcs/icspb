cmake_minimum_required(VERSION 3.22)

project(icspb CXX)

if(NOT ICSPB_BOOTSTRAP_DIR)
	file(TO_CMAKE_PATH "${CMAKE_CURRENT_BINARY_DIR}/_bootstrap" ICSPB_BOOTSTRAP_DIR)
endif()

if(NOT protobuf_ROOT)
	set(ICSPB_BOOTSTRAP_PROTOBUF_VERSION 33.0)
	file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_DIR}/protobuf/${ICSPB_BOOTSTRAP_PROTOBUF_VERSION}" ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR)
	file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}/source" ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR)

	string(TOLOWER "${CMAKE_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR}" ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ID)
	file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}/${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ID}" ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR)
	file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/build" ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR)
	file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/install" ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR)

	file(MAKE_DIRECTORY "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}")

	if(NOT EXISTS "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.stamp")
		message(CHECK_START "Downloading protobuf")
		file(REMOVE_RECURSE "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.tar.gz" "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}/protobuf-${ICSPB_BOOTSTRAP_PROTOBUF_VERSION}" "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}")
		file(REMOVE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}.stamp") # remove next step
		file(DOWNLOAD "https://github.com/protocolbuffers/protobuf/releases/download/v${ICSPB_BOOTSTRAP_PROTOBUF_VERSION}/protobuf-${ICSPB_BOOTSTRAP_PROTOBUF_VERSION}.tar.gz" "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.tar.gz")
		file(ARCHIVE_EXTRACT INPUT "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.tar.gz" DESTINATION "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}")
		file(REMOVE "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.tar.gz")
		file(RENAME "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}/protobuf-${ICSPB_BOOTSTRAP_PROTOBUF_VERSION}" "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}")
		file(TOUCH "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}.stamp")
		message(CHECK_PASS "done")
	endif()

	if(NOT EXISTS "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}.stamp")
		message(CHECK_START "Building protobuf")
		file(REMOVE_RECURSE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}")
		file(REMOVE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}.stamp") # remove next step
		execute_process(
			COMMAND
				"${CMAKE_COMMAND}"
				-G "${CMAKE_GENERATOR}"
				-D "CMAKE_C_COMPILER=${CMAKE_C_COMPILER}"
				-D "CMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}"
				-D CMAKE_BUILD_TYPE=Release
				-D CMAKE_POSITION_INDEPENDENT_CODE=ON
				-D CMAKE_MSVC_RUNTIME_LIBRARY=MultiThreaded
				-D "CMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}"
				-D "CMAKE_INSTALL_PREFIX=${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}"
				-D protobuf_BUILD_TESTS=OFF
				-D protobuf_BUILD_SHARED_LIBS=OFF
				-S "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}"
				-B "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}"
			OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/configure.log"
			ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/configure.log"
			COMMAND_ERROR_IS_FATAL ANY
		)
		execute_process(
			COMMAND
				"${CMAKE_COMMAND}"
				--build "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}"
			OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/build.log"
			ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/build.log"
			COMMAND_ERROR_IS_FATAL ANY
		)
		file(TOUCH "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}.stamp")
		message(CHECK_PASS "done")
	endif()

	if(NOT EXISTS "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}.stamp")
		message(CHECK_START "Installing protobuf")
		file(REMOVE_RECURSE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}")
		execute_process(
			COMMAND
				"${CMAKE_COMMAND}"
				--install "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_BUILD_DIR}"
			OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/install.log"
			ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_ROOT_DIR}/install.log"
			COMMAND_ERROR_IS_FATAL ANY
		)
		file(TOUCH "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}.stamp")
		message(CHECK_PASS "done")
	endif()

	set(protobuf_ROOT "${ICSPB_BOOTSTRAP_PROTOBUF_TARGET_INSTALL_DIR}")

	if(CMAKE_CROSSCOMPILING)
		string(TOLOWER "${CMAKE_HOST_SYSTEM_NAME}-${CMAKE_HOST_SYSTEM_PROCESSOR}" ICSPB_BOOTSTRAP_PROTOBUF_HOST_ID)
		file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_ROOT_DIR}/${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ID}" ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR)
		file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/build" ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR)
		file(TO_CMAKE_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/install" ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR)

		file(MAKE_DIRECTORY "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}")

		# stash env
		set(CXX_ORIG "$ENV{CXX}")
		set(CC_ORIG "$ENV{CC}")
		set(CFLAGS_ORIG "$ENV{CFLAGS}")
		set(CXXFLAGS_ORIG "$ENV{CXXFLAGS}")
		set(LDFLAGS_ORIG "$ENV{LDFLAGS}")
		unset(ENV{CC})
		unset(ENV{CXX})
		unset(ENV{CFLAGS})
		unset(ENV{CXXFLAGS})
		unset(ENV{LDFLAGS})

		if(NOT EXISTS "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}.stamp")
			message(CHECK_START "Building protoc")
			file(REMOVE_RECURSE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}")
			file(REMOVE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}.stamp") # remove next step
			execute_process(
				COMMAND
					"${CMAKE_COMMAND}"
					-G "${CMAKE_GENERATOR}"
					-D CMAKE_BUILD_TYPE=Release
					-D "CMAKE_INSTALL_PREFIX=${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}"
					-D protobuf_BUILD_TESTS=OFF
					-D protobuf_BUILD_SHARED_LIBS=OFF
					-D protobuf_WITH_ZLIB=OFF
					-S "${ICSPB_BOOTSTRAP_PROTOBUF_SOURCE_DIR}"
					-B "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}"
				OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/configure.log"
				ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/configure.log"
				COMMAND_ERROR_IS_FATAL ANY
			)
			execute_process(
				COMMAND
					"${CMAKE_COMMAND}"
					--build "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}"
				OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/build.log"
				ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/build.log"
				COMMAND_ERROR_IS_FATAL ANY
			)
			file(TOUCH "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}.stamp")
			message(CHECK_PASS "done")
		endif()

		if(NOT EXISTS "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}.stamp")
			message(CHECK_START "Installing protoc")
			file(REMOVE_RECURSE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}")
			execute_process(
				COMMAND
					"${CMAKE_COMMAND}"
					--install "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_BUILD_DIR}"
				OUTPUT_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/install.log"
				ERROR_FILE "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_ROOT_DIR}/install.log"
				COMMAND_ERROR_IS_FATAL ANY
			)
			file(TOUCH "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}.stamp")
			message(CHECK_PASS "done")
		endif()

		list(PREPEND CMAKE_PREFIX_PATH "${ICSPB_BOOTSTRAP_PROTOBUF_HOST_INSTALL_DIR}")

		set(ENV{CC} "${CC_ORIG}")
		set(ENV{CXX} "${CXX_ORIG}")
		set(ENV{CFLAGS} "${CFLAGS_ORIG}")
		set(ENV{CXXFLAGS} "${CXXFLAGS_ORIG}")
		set(ENV{LDFLAGS} "${LDFLAGS_ORIG}")
	endif()
endif()

find_package(protobuf REQUIRED CONFIG)

if(CMAKE_CROSSCOMPILING)
	find_program(ICSPB_NATIVE_PROTOC protoc REQUIRED)
	set_target_properties(protobuf::protoc PROPERTIES
		IMPORTED_LOCATION_RELEASE "${ICSPB_NATIVE_PROTOC}"
	)
endif()

file(TO_CMAKE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/protos PROTO_IN_DIR)
file(TO_CMAKE_PATH ${CMAKE_CURRENT_BINARY_DIR}/protos PROTO_OUT_DIR)

add_library(icspb OBJECT
	"${PROTO_IN_DIR}/commands/generic/v1/client_id.proto"
	"${PROTO_IN_DIR}/commands/network/v1/mutex.proto"
	"${PROTO_IN_DIR}/commands/network/v1/networks.proto"
	"${PROTO_IN_DIR}/common/v1/proto_header.proto"
	"${PROTO_IN_DIR}/settings/common/v1/types.proto"
	"${PROTO_IN_DIR}/settings/manufacturing/v1/mfg_config.proto"
)
add_library(icspb::icspb ALIAS icspb)
set_property(TARGET icspb PROPERTY POSITION_INDEPENDENT_CODE ON)

target_link_libraries(icspb PUBLIC protobuf::libprotobuf)
target_include_directories(icspb PUBLIC ${PROTO_OUT_DIR})

protobuf_generate(
    TARGET icspb
    IMPORT_DIRS "${PROTO_IN_DIR}"
    PROTOC_OUT_DIR "${PROTO_OUT_DIR}"
)
